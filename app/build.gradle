plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

//Duración de los json web tokens
def tokenDuration = "5"
//Clave secreta con encriptación que se usa para firmar los tokens
def encKey = Base64.encoder.encode(("f9n94nDMI8YB9df93nFOwmvn75bLE93mJ89n322nMIFO32Mfn3ofj4Nf74Nfcr3M").getBytes())
def secretKey = "\"${new String(encKey)}\""

/*Nombre de la aplicación.*/
def name = "Mis Datos"
/*Clave de la aplicación.*/
def applicationKey = "htUlDdfGBjszGLxVc2p82I3zPQbn8qtfguCrlcd0TddiCkJkrKDf4uUZO73btMYrGAi0tuWkc9Gaipvw"
/*Audiencia.*/
def audience = "https://licenses.apps-smartsolutions.com"

/*Contenido encriptado que contiene
* el nombre, la audiencia y la clave
* de la aplicación.*/
def s = "$name;$audience;$applicationKey"
def enc = Base64.encoder.encode(s.getBytes())
def content0 = "\"${new String(enc)}\""

android {
    compileSdkVersion 30
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.smartsolutions.paquetes"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 32
        versionName "4.1.1"
        archivesBaseName = "$applicationId-v$versionCode"

        //testInstrumentationRunner "androidx.test.runner.Andr
        // oidJUnitRunner"
        testInstrumentationRunner "com.smartsolutions.paquetes.HiltTestRunner"

        resConfigs "es", "en"
    }
    buildFeatures {
        viewBinding true
    }

    buildFeatures.dataBinding = true

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SECRET_KEY", secretKey
            buildConfigField "int", "TOKEN_DURATION", tokenDuration
            buildConfigField "String", "CONTENT_0", content0
            buildConfigField "String", "REGISTRATION_SERVER_URL", "\"https://licenses.apps-smartsolutions.com/api/\""
            buildConfigField "String", "APKLIS_URL", "\"https://apklis.cu/application/com.smartsolutions.paquetes\""
            buildConfigField "String", "TELEGRAM_CHANNEL", "\"https://t.me/joinchat/EVsmSbeOo8diYmJh\""
            buildConfigField "String", "DEVELOPERS_EMAIL", "\"apps.smartsolutions.cuba@gmail.com\""
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SECRET_KEY", secretKey
            buildConfigField "int", "TOKEN_DURATION", tokenDuration
            buildConfigField "String", "CONTENT_0", content0
            //buildConfigField "String", "REGISTRATION_SERVER_URL", "\"http://192.168.251.134/SmartsolutionsApps/api/\""
            buildConfigField "String", "REGISTRATION_SERVER_URL", "\"https://licenses.apps-smartsolutions.com/api/\""
            buildConfigField "String", "APKLIS_URL", "\"https://apklis.cu/application/com.smartsolutions.paquetes\""
            buildConfigField "String", "TELEGRAM_CHANNEL", "\"https://t.me/joinchat/EVsmSbeOo8diYmJh\""
            buildConfigField "String", "DEVELOPERS_EMAIL", "\"apps.smartsolutions.cuba@gmail.com\""
        }
    }

    flavorDimensions "oficial"
    productFlavors {

        oficial {
            dimension "oficial"
            archivesBaseName = "$applicationId-v$versionCode"
        }

        beta {
            dimension "oficial"
            applicationIdSuffix ".beta"
            versionCode 1
            versionName "Beta 1"
            archivesBaseName = "Mis Datos Beta-$versionName"
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    kapt {
        javacOptions {
            // These options are normally set automatically via the Hilt Gradle plugin, but we
            // set them manually to workaround a bug in the Kotlin 1.5.20
            option("-Adagger.fastInit=ENABLED")
            option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
        }
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation project(path: ':vpncore')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "androidx.work:work-runtime-ktx:2.5.0"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    //Three party libraries
    implementation 'nl.dionsegijn:konfetti:1.3.2'
    implementation 'moe.feng.common:material-stepper-view:0.2.5'
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.0'
    implementation 'com.stephentuso:welcome:1.4.1'
    implementation 'org.apache:commons-lang:2.4'
    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'
    implementation 'com.github.warkiz.widget:indicatorseekbar:2.1.2'

    //Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-work:1.0.0"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    //Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    //Room database
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //Jsonwebtoken
    api 'io.jsonwebtoken:jjwt-api:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
    runtimeOnly('io.jsonwebtoken:jjwt-orgjson:0.11.2') {
        exclude group: 'org.json', module: 'json'
    }

    //Zxing
    implementation('com.journeyapps:zxing-android-embedded:4.1.0') { transitive = false }
    implementation 'com.google.zxing:core:3.3.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
}