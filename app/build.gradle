plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

/**
 * Número de la versión oficial. Cambie la versión aquí.
 */
def officialVersionCode = 38

/**
 * Nombre de la versión oficial.
 */
def officialVersionName = "4.4.0"

android {
    compileSdkVersion 32
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.smartsolutions.paquetes"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode officialVersionCode
        versionName officialVersionName
        archivesBaseName = "Mis Datos-$versionName-v$versionCode"

        testInstrumentationRunner "com.smartsolutions.paquetes.HiltTestRunner"

        resConfigs "es", "en"
    }
    buildFeatures {
        viewBinding true
    }

    buildFeatures.dataBinding = true

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "GITHUB_URL", "\"https://github.com/Abdavid92/Datwall\""
            buildConfigField "String", "APKLIS_URL", "\"https://apklis.cu/application/com.smartsolutions.paquetes\""
            buildConfigField "String", "DEVELOPERS_EMAIL", "\"apps.smartsolutions.cuba@gmail.com\""
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "GITHUB_URL", "\"https://github.com/Abdavid92/Datwall\""
            buildConfigField "String", "APKLIS_URL", "\"https://apklis.cu/application/com.smartsolutions.paquetes\""
            buildConfigField "String", "DEVELOPERS_EMAIL", "\"apps.smartsolutions.cuba@gmail.com\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    kapt {
        javacOptions {
            // These options are normally set automatically via the Hilt Gradle plugin, but we
            // set them manually to workaround a bug in the Kotlin 1.5.20
            option("-Adagger.fastInit=ENABLED")
            option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
        }
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation project(path: ':vpncore')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    //Three party libraries
    implementation 'moe.feng.common:material-stepper-view:0.2.5'
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.0'
    implementation 'com.stephentuso:welcome:1.4.1'
    implementation 'org.apache:commons-lang:2.4'
    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'
    implementation 'com.github.warkiz.widget:indicatorseekbar:2.1.2'
    implementation 'com.github.Z17-CU:apklisupdate:v1.4'

    //Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-work:1.0.0"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    //Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    /** rx java **/
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.8.1'

    //html
    implementation 'org.sufficientlysecure:html-textview:3.9'
    implementation 'com.squareup.picasso:picasso:2.71828'

    //Room database
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //Zxing
    implementation('com.journeyapps:zxing-android-embedded:4.1.0') { transitive = false }
    implementation 'com.google.zxing:core:3.3.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
}